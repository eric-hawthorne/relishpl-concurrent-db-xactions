origin   relish.pl2012
artifact relish_website
package  model

"""
 model.rel

 Data types used to represent relish code origins and other entities associated with sharing relish code.
"""


import
   strings


Origin
"""
 A relish code origin that has registered at shared.relish.pl.
"""
   ident String
   contactGivenName String
   contactFamilyName String
   organization String
   department String
   email String
   pwHash String
   isPublic Bool
   isVerified Bool  // true if email sent to origin email address was confirmed received
   emailVerificationToken String  // used to verify that an email sent to the origin was received 
   publicKeyCert String
   registered Time


originIdError originId String > String
"""
 Checks that the origin id has valid syntax and is not a duplicate of an already registered origin.
 Returns "" if origin is ok, else an error message.
"""
   if not originId
      => "Please enter an origin id. e.g. foo.org2013"
   if neq originId (lower originId)
      => "The origin id must be lowercase."
   if lt (index originId ".") 1
      => "The origin id must include an internet domain name. Example: foo.org2013"
   // TODO: Need regexp matching library in relish
   if exists originId
      => cat "The origin '" originId "' is already registered. Please choose another origin id."
   => ""
