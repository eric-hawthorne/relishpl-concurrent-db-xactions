origin   shared.relish.pl2012
artifact relish_lib
package  reflect

""" 
 reflect.rel

 Types and methods for reflection on the relish language structure (metadata) of data in relish programs. 

 Note. This is pretty conservative right now. There are no data modification or method execution provisions
 in the reflection library yet. Just interrogating the structure.

 Note 2: Once public/private attribute and method classification comes into the language,
 protection will need to be imlpemented in this reflection library to ensure it is not a way around
 privacy protections.
"""


DataType
"""
 Represents a relish datatype.
 Can list the attributes specified by the type, or all attributes specified in the closure of the type and 
 all of its direct and indirect supertypes.
"""
   name String


Attribute
"""
 Represents an attribute descriptor of a datatype, or one direction of a bi-directional type-relation descriptor.
"""
   name String
   type DataType
   minArity Int 
   maxArity Int
   inverse ?Attribute


SimpleAttrDescriptor
"""
 A descriptor of a unary primitive attribute and its value for some object instance.
 The value has been converted to type String.
"""
   attrName String
   typeName String
   val String


ComplexAttrDescriptor
"""
 A descriptor of an attribute and its value for som object instance,
 where the attribute is not a unary attribute or does not have a atomic primitive as value type.
 The values have been converted to type String.
 If valIsObject is true, the values are reflectIds of objects, from which other reflection libary methods
 are able to fetch the object and its attributes.
"""
   attrName String
   typeName String
   minArity Int
   maxArity Int
   valIsObject Bool
   valIsCollection Bool  // valIsObject will also be true - this is used with single-val attr w. collection type
   inverseAttrName String  // "" if there is no inverse
   inverseMinArity Int
   inverseMaxArity Int    
   vals 0 N [] String


ObjectTree
"""
 Represents a whole tree of associated objects, out to the specified depth.
 depth 1 is a single object with simple attributes only.
 depth 2 includes directly associated objects
 depth 3 includes associates of those.
"""
   depth Int
   rootReflectId String
   objects {} String > ObjectDescriptor


ObjectDescriptor
"""
 A descriptor a relish structured object.
 May include only the type and ids of the object,
 or also the simple attributes (unary, atomic-primitive-valued attributes)
 and/or also the complex attributes (multi-valued and/or object-valued attributes)
"""
   typeName String  // The full name of the type of the object, although in backwards (human readable) format.
   reflectId String  // The id for the object that is used by the reflection api.
   dbid String  // String representation of the dbid of the object, or "" if the object is not locally persistent.
   uuid String  // The uuidStr() value of the object, or "" if the object is not persistent somewhere.
   includesSimpleAttrs Bool
   includesComplexAttrs Bool
   simpleAttrs [] SimpleAttrDescriptor
   complexAttrs [] ComplexAttrDescriptor 


getObjectTree reflectId String depth Int > tree ObjectTree
"""
 Returns an object tree to the specified depth.
 See definition of ObjectTree.
"""
   tree = ObjectTree
   tree.rootReflectId = reflectId
   tree.depth = depth
   tree.objects = {}String > ObjectDescriptor

   populateObjectTree tree reflectId depth        


populateObjectTree tree ObjectTree reflectId String depth Int
"""
 Helper function.
 Add object descriptors to the tree up to the needed depth.
 Recursive.
"""
   if not tree.objects[? reflectId]
      obj = objectByReflectId reflectId 
      objDesc = makeDescriptor obj reflectId depth
      tree.objects[reflectId] = objDesc 
      if gt depth 1
         depth = minus depth 1
         for complexAttrDesc in objDesc.complexAttrs
            if complexAttrDesc.valIsObject
               for associatedReflectId in complexAttrDesc.vals
                  populateObjectTree tree associatedReflectId depth


makeDescriptor obj Struct reflectId String remainingDepth Int > objDesc ObjectDescriptor      
"""
 If remainingDepth is 0, does not even include simple attributes.
 If remainingDepth is 1, includes only simple attributes
 If remainingDepth is 2 or more, includes also complex attributes
"""
   objType = typeOf obj

   objDesc = ObjectDescriptor
   objDesc.typeName = backwardsTypeName objType.name
   objDesc.reflectId = reflectId
   if hasUuid obj
      objDesc.dbid = String dbid obj
      objDesc.uuid = uuidStr obj
   else
      objDesc.dbid = ""
      objDesc.uuid = ""
   if gt remainingDepth 0
      objDesc.simpleAttrs = getSimpleAttributes reflectId
      objDesc.includesSimpleAttrs = true
   if gt remainingDepth 1
      objDesc.complexAttrs = getComplexAttributes reflectId    
      objDesc.includesComplexAttrs = true  