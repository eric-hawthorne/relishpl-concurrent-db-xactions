origin   shared.relish.pl2012
artifact relish_tests
package  concurrency 

""" 
 concurrency.rel

 Test go-routines, Channels, and Mutex locks.
"""


import
   strings
   datetime
   test


Buffer
"""
 Implements a String buffer
"""
   s 0 N [] String


spew b Buffer mx Mutex chin InChannel inNum Int chout OutChannel outNum Int n Int
"""
 Exchanges data on channels and prints out what it is doing.
"""
   i = 0
   while lt i 2000
      i = plus i 1 
      lock mx
      b.s += cat i " about to send " n " to ch " outNum
      unlock mx      
      chout <- n  
      lock mx 
      b.s += cat i " sent " n " to ch " outNum
      b.s += cat i " about to receive something from ch " inNum      
      unlock mx      
      m = <- chin
      lock mx    
      b.s += cat i " got " m " from ch " inNum
      unlock mx


main
"""
 Main program.
"""
   testConcurrency


testConcurrency
"""
 Tests Mutexes, Channels, and go-routines. 

 This program keeps running and spewing output to stdout.
"""
   passed = true
   
   print "===== Concurrency"

   passed = and assert (testOwnedMutexes) "OwnedMutexes"
                passed
   
   b = Buffer  

   ch1 = Channel
            1
   ch2 = Channel
   mx = Mutex

   go spew b mx ch1 1 ch2 2 5

   spew b mx ch2 2 ch1 1 3

   sleep seconds 3

   passed = and assertEq (len b.s) 16000 "number of messages through channels"
                passed
   if passed             
      print "PASSED"
      
      
testOwnedMutexes > Bool
"""
 Test mutexes of type OwnedMutex, which are owned by whichever thread (goroutine) has a lock on them.
 That thread is free to lock again a mutex that it has already got locked,
 because the mutex remembers which thread has it locked, and, for nested lockings by
 the same thread, it just increments a "number of times I am locked nestedly by this
 thread" counter. The thread must of course still unlock the mutex exactly as many times
 as it nestedly locked the mutex.
"""

   b = Buffer
   mx = OwnedMutex
   
   go doLockedThing b mx 1
   go doLockedThing b mx 2
   doLockedThing b mx 3
   
   sleep seconds 3
   
   => eq (len b.s) 9
      

doLockedThing b Buffer m OwnedMutex i Int
"""
 Just nestedly lock and unlock the mutex.
"""
   pause = div (seconds 1) 10
   for j = 1   lt j 4   j = plus j 1
      lock m
      lock m
      lock m
      b.s += fill "%s:%s" i j
      unlock m
      unlock m
      unlock m
      sleep pause