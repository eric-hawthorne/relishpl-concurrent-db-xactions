origin   relish.pl2012
artifact relish_tests
package  concurrency 

""" 
 concurrency.rel

 Test go-routines, Channels, and Mutex locks.
"""


spew mx Mutex chin InChannel inNum Int chout OutChannel outNum Int n Int
"""
 Exchanges data on channels and prints out what it is doing.
"""
   i = 0
   while lt i 2000
      i = plus i 1 
      lock mx
      print i "about to send" n "to ch" outNum
      unlock mx      
      chout <- n  
      lock mx 
      print i "sent" n "to ch" outNum
      print i "about to receive something from ch" inNum      
      unlock mx      
      m = <- chin
      lock mx    
      print i "got" m "from ch" inNum
      unlock mx


main
"""
 Main program.
"""
   testConcurrency


testConcurrency
"""
 Tests Mutexes, Channels, and go-routines. 

 This program keeps running and spewing output to stdout.
"""
   ch1 = Channel
            1
   ch2 = Channel
   mx = Mutex

   go spew mx ch1 1 ch2 2 5

   spew mx ch2 2 ch1 1 3

